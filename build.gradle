plugins {
    id "com.github.spotbugs" version "5.0.0-beta.1"
    id "org.sonarqube" version "3.3"
    id 'checkstyle'
    id 'jacoco'
    id 'java'
    id 'pmd'
}

group 'ch.addere.osv'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.12.5'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.5'
    implementation group: 'com.github.package-url', name: 'packageurl-java', version: '1.4.1'
    implementation group: 'de.skuzzle', name: 'semantic-version', version: '2.1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.20.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

checkstyle {
    toolVersion '9.0'
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

pmd {
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacoco {
    toolVersion = "0.8.7"
    reportsDirectory = layout.buildDirectory.dir('reports/coverage')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "ryru_osv"
        property "sonar.organization", "ryru"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
